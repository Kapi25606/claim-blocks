plugins {
    id "org.jetbrains.kotlin.jvm" version "1.1.2-5"
    id "org.jetbrains.kotlin.kapt" version "1.1.2-5"
    id "com.qixalite.spongestart" version "1.6.2"
    id "com.github.johnrengelman.shadow" version "1.2.4"
    id "flavor.pie.promptsign" version "1.0.2"
}

group "de.randombyte"
version "1.2.4"

repositories {
    jcenter()
    maven { url "https://repo.spongepowered.org/maven/" }
    maven { url "https://jitpack.io" }
    maven { url "http://files.minecraftforge.net/maven" }
}

spongestart {
    eula true
    spongeForgeVersion "1.10.2-2281-5.2.0-BETA-2274"
}

configurations {
    compile.extendsFrom shadow
    compile.extendsFrom kapt
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jre8:1.1.2-5"
    kapt "org.spongepowered:spongeapi:5.1.0"
    shadow("com.github.randombyte-developer.kosp:kosp:v0.5.2") { transitive = false }

    compile "me.ryanhamshire:griefprevention:1.10.2-4.0.1.490:api"
    compile files("libs/RedProtect-Sponge-6.1.0-7.2.0-b137.jar")
    compile files("libs/fg.jar") // foxguard
    compile(files("libs/fc.jar")) // foxcore
}

jar.enabled = false

shadowJar {
    configurations = [project.configurations.shadow]

    exclude "StartServer*.*"

    relocate "kotlin", "de.randombyte.claimblocks.shaded.kotlin"
    relocate "de.randombyte.kosp", "de.randombyte.claimblocks.shaded.kosp"

    classifier = null // Remove '-all' suffix from output file name
}
build.dependsOn shadowJar
signArchives.dependsOn shadowJar

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}